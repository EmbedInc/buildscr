//   Build everything from this source directory.
//
//   This source directory contains scripts used for building software.  These
//   are copied into the (cog)bat directory if it exists, or the (cog)com
//   directory otherwise.
//
var new destdir string       //destination directory
var new dest string          //destination file name
var new gnam string          //generic name of file
var new handled bool         //directory entry was handled
//
//   Set DESTDIR to the destination directory.  This will be the Embed BAT
//   directory if it exists, otherwise the Embed COM directory.
//
set destdir "(cog)bat"
if [not [exist destdir fnam]] then
  set destdir "(cog)com"
  if [not [exist destdir fnam]] then
    show "Embed software not installed, or the installation is corrupted."
    stop 3
    endif
  endif
//
//   Make sure the Embed LIB directory exists.
//
dir exist [tnam "(cog)lib"]
//
//   Loop thru all the files in this directory.  Install all ".bat" and ".escr"
//   files.
//
dir "(cog)source/buildscr"
loop dir "." file
  //
  //   Check for specific files to not install.
  //
  if [= [dent] "build.escr"] then
    repeat
    endif
  //
  //   Handle files that require special processing.
  //
  set handled true
  pick one by [dent]

  optionelse
    set handled false
    endpick
  if handled then
    repeat
    endif
  //
  //   Ignore this file if not .BAT or .ESCR.
  //
  set gnam [lnam [dent] ".bat" ".escr"]
  if [= [slen gnam] [slen [dent]]] then
    repeat
    endif
  //
  //   Install this file into the BAT or COM directory, as appropriate for
  //   scripts on this machine.
  //
  set dest [str destdir "/" [dent]] //make destination file name
  if [exist dest fnam] then  //destination file already exists ?
    if [= [file dtm dest] [file dtm [dent]]] then //same file ?
      repeat
      endif
    endif
  run "copyt.exe -from " [dent] " -to " dest
  endloop
